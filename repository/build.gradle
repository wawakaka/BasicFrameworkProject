plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
}

apply from: "$rootDir/dep/build-lib.gradle"

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

android {
    namespace 'io.github.wawakaka.repository'
    buildFeatures {
        buildConfig true
    }
    defaultConfig {
        buildConfigField "String", "API_KEY", "\"${localProperties.getProperty("API_KEY") ?: project.findProperty("API_KEY") ?: ""}\""
        buildConfigField "String", "BASE_URL", "\"${localProperties.getProperty("BASE_URL") ?: project.findProperty("BASE_URL") ?: ""}\""
    }
}

dependencies {
    implementation project(':restapi')

    // Kotlin Coroutines
    implementation libs.kotlinx.coroutines.core

    // Networking
    implementation libs.retrofit
    implementation libs.gson
}
